#version 430

layout( local_size_x = 10, local_size_y = 10 ) in;

layout( binding = 0, rgba8) uniform image2D ColorImg;

uniform int width = 800;
uniform int height = 600;

struct Ray{
	vec3 Origin;
	vec3 Direction;
};

float hit_sphere(vec3 center,float radius,in Ray r)
{
	vec3 oc = r.Origin - center;
    float a = dot(r.Direction, r.Direction);
    float b = 2.0f * dot(oc, r.Direction);
    float c = dot(oc,oc)-radius * radius;
    float discriminant = b * b - 4 * a * c;
	if (discriminant < 0){
		return -1.0;
	}
    return (-b - sqrt(discriminant)) / (2.0 * a);
}

vec3 point_at_parameter(float t, in Ray r){
	return r.Origin + t * r.Direction;
}


vec3 Color(Ray r) {
    vec3 unit_direction = normalize(r.Direction);
	float t = hit_sphere(vec3(0, 0, -1), 0.5f, r);
	if (t > 0)
    {
		vec3 N = normalize(point_at_parameter(t,r) - vec3(0,0,-1));
		return 0.5 * vec3(N.x + 1 , N.y + 1, N.z + 1);
    }
    t = 0.5f * (unit_direction.y + 1.0f);
    return (1.0f - t) * vec3(1.0f, 1.0f, 1.0f) + t * vec3(0.5f,0.7f,1.0f);
}

void main() {
	vec3 lower_left_corner = vec3(-2.0f,-1.0f,-1.0f);
    vec3 horizontal = vec3(4.0f,0.0f,0.0f);
    vec3 origin = vec3(0.0f,0.0f,0.0f);
    vec3 vertical = vec3(0.0f,2.0f,0.0f);

	float i = gl_GlobalInvocationID.x;
	float j = height - gl_GlobalInvocationID.y;
	float u = i / width;
    float v = j / height;
	vec3 color = Color(Ray(origin,lower_left_corner+ u *horizontal+v*vertical));
	imageStore(ColorImg,ivec2(gl_GlobalInvocationID.xy), vec4(color,1));
}
